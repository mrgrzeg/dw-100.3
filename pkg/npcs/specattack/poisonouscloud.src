//-----------------------------------------------------------------------------

include "include/attack";
include "include/effects";
include "include/coords";
include "include/objects/location";
include "include/attributes/vitals";
include "include/mobile/npcs/specs";
include ":magic:magicatt";

//-----------------------------------------------------------------------------

program PoisonousCloudEffect(Params)

  var me := Params[1];
  var Mobs := Params[2];
  var Spec := Params[3];

  var Range := Spec[5];

  var maxRange := Min(Range, 20);
  var i;
  var srcLoc := GetLocStruct(me);
  var dmgMobs := array;
  
  for(i:=1; i<=maxRange; i+=1)
    var Locs := GetLocsInCircumference(srcLoc, i);

    foreach Loc in Locs
      if(Spec[7] == "dragon")
        PlayPcStEffect(Loc.x, Loc.y, Loc.z, me.realm, FX_SMOKE, 68, 0x20, 0x20, 4);
      else
        PlayPcStEffect(Loc.x, Loc.y, Loc.z, me.realm, FX_SMOKE, 68, 0x20, 0x20, 4);
        PlaySoundEffect(me, SFX_FART);
        PlaySoundEffect(me, SFX_SMOKE_BOMB);
      endif
    endforeach
    
    foreach Mob in Mobs
      if(!(Mob in dmgMobs))
        if(coordist(srcLoc.x, srcLoc.y, Mob.x, Mob.y) <= i)
          dmgMobs.append(Mob);
          DealPoisonousCloudDamage(me, Mob, Spec);
        endif
      endif
    endforeach
    
    sleepms(300);
  endfor

  foreach Mob in Mobs
    if(!(Mob in dmgMobs))
      DealPoisonousCloudDamage(me, Mob, Spec);
    endif
  endforeach

endprogram

//-----------------------------------------------------------------------------

function DealPoisonousCloudDamage(me, Mob, Spec)
  
  var Dmg;
  var StamDmg := __IfElse(!Spec[9], 95, Spec[9]);

  if(IsInSpecArea(GetSpecAreaIdx(Mob), GetSpecAreaIdx(me)))
    Dmg := CInt(GetHp(Mob) * Spec[6]);
    DealManaDamage(Mob, CInt(GetMana(Mob)  / 2), me);
    DealStaminaDamage(Mob, StamDmg, me);
  elseif(Mob.isa(POLCLASS_NPC))
    Dmg := GetHp(Mob)-1;
  else
    Dmg := GetHp(Mob)+1;
    DealManaDamage(Mob, CInt(GetMana(Mob)  / 2), me);
    DealStaminaDamage(Mob, StamDmg, me);
  endif

  if(IsPlayer(Mob))
    Dmg := __IfElse(!Spec[6], Dmg, CInt(PercentInterpolate(GetHp(Mob), Spec[6])));
  endif

  var Dist := coordist(me.x, me.y, Mob.x, Mob.y);

  if(Dist > ODLEGLOSC_OD_MOBA)
    Dmg += Dist * 100;
  endif

  if(Dmg > 0)
    if(!Spec[8])
      DoMagicDamage(Mob, me, Dmg, 14, DMGTYPE_POISON);
    else
      ApplyRawDamage(Mob, Dmg);
    endif
    PlayPcEffect(Mob, FX_SMOKE, 0x10, 0x10, 1, Mob.realm);
  endif

endfunction

//-----------------------------------------------------------------------------
