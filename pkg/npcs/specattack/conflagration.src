//-----------------------------------------------------------------------------

include "include/attack";
include "include/effects";
include "include/coords";
include "include/objects/location";
include "include/attributes/vitals";
include "include/mobile/npcs/specs";
include ":magic:magicatt";

//-----------------------------------------------------------------------------

program ConflagrationEffect(Params)

  var me := Params[1];
  var Mobs := Params[2];
  var Spec := Params[3];

  var Range := Spec[5];

  var maxRange := Min(Range, 20);
  var i;
  var srcLoc := GetLocStruct(me);
  var dmgMobs := array;
  
  for(i:=1; i<=maxRange; i+=1)
    var Locs := GetLocsInCircumference(srcLoc, i);

    foreach Loc in Locs
      if(Spec[7] == "dragon")
        PlayStationaryEff(Loc.x, Loc.y, Loc.z, FX_EXPLODE_2, 0x20, 0x20, 1, me.realm);
        PlaySoundEffect(me, SFX_IMMOLATION);
      elseif(Spec[7] == "immolation")
        PlayStationaryEff(Loc.x, Loc.y, Loc.z, FX_FLAMESTRIKE, 0x20, 0x20, 1, me.realm);
        PlaySoundEffect(me, SFX_IMMOLATION);
      elseif(Spec[7] == "Cold")
        PlayStationaryEff(Loc.x, Loc.y, Loc.z, FX_SMOKE , 0x5, 0x10, 1, me.realm);
        PlaySoundEffect(me, SFX_IMMOLATION);
      elseif(Spec[7] == "Electric")
        PlayStationaryEff(Loc.x, Loc.y, Loc.z, FX_STATIC, 0x20, 0x10, 10, me.realm);
        PlaySoundEffect(me, SFX_IMMOLATION);
      elseif(Spec[7] == "Mrok")
        PlayStationaryEff(Loc.x, Loc.y, Loc.z, FX_GLOW_SPIKE, 0x20, 0x20, 1, me.realm);
        PlaySoundEffect(me, SFX_IMMOLATION);
      endif
    endforeach
    
    foreach Mob in Mobs
      if(!(Mob in dmgMobs))
        if(coordist(srcLoc.x, srcLoc.y, Mob.x, Mob.y) <= i)
          dmgMobs.append(Mob);
          DealConflagrationDamage(me, Mob, Spec);
        endif
      endif
    endforeach
    
    sleepms(300);
  endfor

  foreach Mob in Mobs
    if(!(Mob in dmgMobs))
      DealConflagrationDamage(me, Mob, Spec);
    endif
  endforeach

endprogram

//-----------------------------------------------------------------------------

function DealConflagrationDamage(me, Mob, Spec)
  
  var Dmg;
  var StamDmg := __IfElse(!Spec[9], 95, Spec[9]);

  if(IsInSpecArea(GetSpecAreaIdx(Mob), GetSpecAreaIdx(me)))
    Dmg := CInt(GetHp(Mob) * Spec[6]);
    DealManaDamage(Mob, CInt(GetMana(Mob)  / 2), me);
    DealStaminaDamage(Mob, StamDmg, me);
  elseif(Mob.isa(POLCLASS_NPC))
    Dmg := GetHp(Mob)-1;
  else
    Dmg := GetMaxHp(Mob)+1;
    DealManaDamage(Mob, CInt(GetMana(Mob)  / 2), me);
    DealStaminaDamage(Mob, StamDmg, me);
  endif

  if(IsPlayer(Mob))
    Dmg := __IfElse(!Spec[6], Dmg, CInt(PercentInterpolate(GetHp(Mob), Spec[6])));
  endif

  var Dist := coordist(me.x, me.y, Mob.x, Mob.y);
  if(Dist > ODLEGLOSC_OD_MOBA)
    Dmg += Dist * 100;
  endif

  var NpcdescCfg := ReadConfigFile("::npcdesc")[Mob.npctemplate];

  if(Dmg > 0)
    PlaySoundEffect(Mob, SFX_FIRE_STRIKE);
    if(Spec[7]=="dragon")
      if(!Spec[8])
        if(IsTamed(Mob) and NpcdescCfg.FireMob)
          HealMobile(Mob, CInt(PercentInterpolate(GetHp(Mob), 50)), me); 
        else
          DoMagicDamage(Mob, me, Dmg, 10, DMGTYPE_FIRE);
        endif
      else
        if(IsTamed(Mob) and NpcdescCfg.FireMob)
          HealMobile(Mob, CInt(PercentInterpolate(GetHp(Mob), 50)), me); 
        else
          ApplyRawDamage(Mob, Dmg);
        endif
      endif
      PlayPcEffect(Mob, FX_EXPLODE_2, 0x10, 0x10, 1, Mob.realm);
    elseif(Spec[7]=="immolation")
      if(!Spec[8])
        if(IsTamed(Mob) and NpcdescCfg.FireMob)
          HealMobile(Mob, CInt(PercentInterpolate(GetHp(Mob), 50)), me); 
        else
          DoMagicDamage(Mob, me, Dmg, 10, DMGTYPE_FIRE);
        endif
      else
        if(IsTamed(Mob) and NpcdescCfg.FireMob)
          HealMobile(Mob, CInt(PercentInterpolate(GetHp(Mob), 50)), me); 
        else
          ApplyRawDamage(Mob, Dmg);
        endif
      endif
      PlayPcEffect(Mob, FX_FLAMESTRIKE, 0x10, 0x10, 1, Mob.realm);
    elseif(Spec[7]=="Cold")
      if(!Spec[8])
        DoMagicDamage(Mob, me, Dmg, 20, DMGTYPE_COLD);
      else
        ApplyRawDamage(Mob, Dmg);
      endif
      PlayPcEffect(Mob, FX_SMOKE, 0x10, 0x10, 1, Mob.realm);
    elseif(Spec[7]=="Electric")
      if(!Spec[8])
        DoMagicDamage(Mob, me, Dmg, 20, DMGTYPE_ELECTRIC);
      else
        ApplyRawDamage(Mob, Dmg);
      endif
      PlayPcEffect(Mob, FX_STATIC, 0x10, 0x10, 1, Mob.realm);
    elseif(Spec[7]=="Mrok")
      if(!Spec[8])
        DoMagicDamage(Mob, me, Dmg, 20, DMGTYPE_POISON);
      else
        ApplyRawDamage(Mob, Dmg);
      endif
      PlayPcEffect(Mob, FX_GLOW_SPIKE, 0x10, 0x10, 1, Mob.realm);
    endif
  endif

endfunction

//-----------------------------------------------------------------------------
