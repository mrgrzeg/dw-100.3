//-----------------------------------------------------------------------------

include "../potion";

//-----------------------------------------------------------------------------

program UseBluePotion(who, potion)

  if(!StandardItemCheck(who,potion,NO_COMBAT_CHECK))
    return;
  endif

  var ExpMod   := GetExpPotionMod(Potion);

  if(ExpMod and !CanMod(who, MOD_DEX))
    SendBad(who, "Ponowne wypicie mikstury nie przyniesie efektu.");
    return;
  endif

  if(!CanDrink(who,potion))
    return;
  endif

  var IncMod   := GetIncreasedQualityMod(Potion); //podwyzszona jakosc, nie exp!!! cos pomiedzy 
  var Strength := GetPotionStrength(potion.objtype);
  var QualMod  := GetPotionQualMod(Potion);

  Strength     := CalcPotStrength(Strength,ExpMod, CInt(GetEffectiveSkill(who, SKILLID_ANATOMY) / 2));
  
  if(potion.objtype == UOBJ_GREATER_AGILITY_POTION and ExpMod)
    Strength := GetExpStrengthMod(ExpMod); //expowa
  elseif(potion.objtype == UOBJ_GREATER_AGILITY_POTION and !ExpMod and IncMod)
    if(IncMod < 28)
      IncMod := 28;
    endif
    Strength := IncMod; //podwyrzszonej jakosci
  elseif(potion.objtype == UOBJ_GREATER_AGILITY_POTION and !ExpMod and !IncMod and GetPotionQuality(Potion))
    Strength := POTQUAL_MATURE_VALUE; //dojzala
  endif

  if(!EmptyBottle(who,potion))
    return;
  endif

  DoMod(who,MOD_DEX,Strength, STANDARD_POTION_MOD_DUR * QualMod + (ExpMod * 30));

endprogram

//-----------------------------------------------------------------------------
